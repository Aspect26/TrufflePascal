COMPILER Pascal

IGNORECASE

CHARACTERS

letter = 'A'..'Z' + 'a'..'z'.
nonZeroDigit = "123456789".
digit = "0123456789".

cr = '\r'.
lf = '\n'.
tab = '\t'.

stringChar = ANY - "'" - cr - lf.

TOKENS

identifier = letter {letter | digit}.
stringLiteral = "'" { stringChar } "'".
integerLiteral = "0" | nonZeroDigit { digit }.
doubleLiteral = ("0" | nonZeroDigit { digit }) "e" ["-" | "+"] { digit } | ("0" | nonZeroDigit { digit }) "." digit { digit } ["e" ["-"|"+"] { digit }].

PRAGMAS

COMMENTS FROM "(*" TO "*)"
COMMENTS FROM "{" TO "}"
IGNORE cr + lf + tab

PRODUCTIONS

Pascal
=
(
    Program
	[ ImportsSection ]
	Declarations
	MainFunction
|
	Unit
)
.


Program
=
    "program"
    identifier                                      (. factory.startPascal(t); .)
    ";"
.



ImportsSection
=
"uses"
identifier										(. factory.registerUnit(t); .)
{
	","
	identifier									(. factory.registerUnit(t); .)
}
";"
.



Declarations
=
[ ConstantDefinitions ]
[ TypeDefinitions ]
[ VariableDeclarations ]
{ Subroutine }
.



TypeDefinitions
=
"type"
TypeDefinition ";"
{
    TypeDefinition ";"
}
.


TypeDefinition
=
identifier										(. Token identifier = t; .)
"="
Type<out TypeDescriptor typeDescriptor>         (. factory.registerNewType(identifier, typeDescriptor); .)
.



Type<out TypeDescriptor typeDescriptor>
=                                               (. typeDescriptor = null; .)
(
	identifier                                  (.  typeDescriptor = factory.getTypeDescriptor(t); .)
|
    EnumDefinition<out typeDescriptor>
|
	ArrayDefinition<. out List<OrdinalDescriptor> ordinalDimensions .>
	{
		IF(continuesArray())
		"of"
		ArrayDefinition<. out List<OrdinalDescriptor> additionalDimensions .>
												(. ordinalDimensions.addAll(additionalDimensions); .)
	}
	"of"
	identifier									(. typeDescriptor = factory.createArray(ordinalDimensions, t); .)
|
    "set" "of"
    Ordinal<out OrdinalDescriptor ordinal>      (. typeDescriptor = factory.createSetType(ordinal); .)
|
    FileType<out typeDescriptor>
|
    RecordType<out typeDescriptor>
)
.


ArrayDefinition<. out List<OrdinalDescriptor> ordinalDimensions .>
=
[ "packed" ]
"array"											(. ordinalDimensions = new ArrayList<>(); .)
"["												(. OrdinalDescriptor ordinalDescriptor = null; .)
Ordinal<out ordinalDescriptor>					(. ordinalDimensions.add(ordinalDescriptor); .)
{
	","
	Ordinal<out ordinalDescriptor>				(. ordinalDimensions.add(ordinalDescriptor); .)
}
"]"
.


EnumDefinition<out TypeDescriptor typeDescriptor>
=                                               (. List<String> enumIdentifiers = new ArrayList<>(); .)
"("
    identifier                                  (. enumIdentifiers.add(factory.getIdentifierFromToken(t)); .)
    {
        ","
        identifier                              (. enumIdentifiers.add(factory.getIdentifierFromToken(t)); .)
    }
")"                                             (. typeDescriptor = factory.registerEnum(enumIdentifiers); .)
.


FileType<out TypeDescriptor fileDescriptor>
=
"file" "of"
Type<out TypeDescriptor contentType>            (. fileDescriptor = factory.createFileType(contentType); .)
.


RecordType<out TypeDescriptor typeDescriptor>
=
"record"                                        (. factory.startRecord(); .)
RecordFieldList<out typeDescriptor>
"end"                                           (. factory.finishRecord(); .)
.


RecordFieldList<out TypeDescriptor typeDescriptor>
=
(
    RecordFixedPart
    [
        IF(recordVariantPartStarts())
        ";"
        RecordVariantPart
    ]
|
    RecordVariantPart
)
[ ";" ]                                         (. typeDescriptor = factory.createRecordType(); .)
.


RecordFixedPart
=
RecordFixedSection
{
    IF(recordFixedPartContinues())
    ";"
    RecordFixedSection
}
.


RecordFixedSection
=
VariableLineDeclaration
.


RecordVariantPart
=
"case"
RecordVariantSelector
"of"
RecordVariants
.


RecordVariantSelector
=
[
IF(isVariantSelectorTag())
identifier ":"
]
identifier                                      (. /* ordinal */ .)
.


RecordVariants
=
CaseConstantList
":"
"("
RecordFieldList<out TypeDescriptor type>
")"
.


CaseConstantList
=
Constant<out ConstantDescriptor constant>
{
    ","
    Constant<out constant>
}
.


Ordinal<out OrdinalDescriptor ordinal>
=												(. ordinal = null; .)
(
    IF(isSubrange())
	SubrangeType<out ordinal>
|
	Type<out TypeDescriptor typeDescriptor>     (. ordinal = factory.castTypeToOrdinalType(typeDescriptor); .)
)
.


SubrangeType<out OrdinalDescriptor ordinal>
=
Constant<out ConstantDescriptor lowerBound>
".."
Constant<out ConstantDescriptor upperBound>     (. ordinal = factory.createSimpleOrdinalDescriptor(lowerBound, upperBound); .)
.


ConstantDefinitions
=
"const"
ConstantDefinition ";"
{
    ConstantDefinition ";"
}
.


ConstantDefinition
=
identifier										(. Token identifier = t; .)
"="
Constant<out ConstantDescriptor constant>       (. factory.registerConstant(identifier, constant); .)
.


Constant<out ConstantDescriptor constant>
=
                                                (. constant = null; String sign = ""; .)
[
    "+" | "-"                                   (. sign = t.val; .)
]
(
	UnsignedIntegerLiteral<out long value>      (. constant = factory.createLongConstant(sign, value); .)
|
    UnsignedDoubleLiteral<out double value>     (. constant = factory.createDoubleConstant(sign, value); .)
|
	StringLiteral<out String value> 			(. constant = factory.createCharOrStringConstant(sign, value); .)
|
    LogicLiteral<out boolean value>             (. constant = factory.createBooleanConstant(sign, value); .)
|
	IdentifierConstant<out Token identifier>    (. constant = factory.createConstantFromIdentifier(sign, identifier); .)
)
.


UnsignedIntegerLiteral<out long value>
=
integerLiteral                                  (. value = factory.getLongFromToken(t); .)
.


UnsignedDoubleLiteral<out double value>
=
doubleLiteral                                   (. value = factory.getDoubleFromToken(t); .)
.


IdentifierConstant<out Token identifierToken>
=
identifier                                      (. identifierToken = t; .)
.


VariableDeclarations
=
"var"
VariableLineDeclaration ";"
{
    VariableLineDeclaration ";"
}
.


VariableLineDeclaration
=
IdentifiersList<. out List<String> identifiers .>
":"
Type<out TypeDescriptor typeDescriptor>         (. factory.registerVariables(identifiers, typeDescriptor); .)
.


IdentifiersList<. out List<String> identifiers .>
=                                               (. identifiers = new ArrayList<>(); .)
identifier                                      (. identifiers.add(factory.getIdentifierFromToken(t)); .)
{
    ","
    identifier                                  (. identifiers.add(factory.getIdentifierFromToken(t)); .)
}
.


Subroutine
=
(
    Procedure ";"
|
    Function ";"
)
.


Procedure
=
"procedure"
identifier                                      (. Token identifierToken = t; .)
                                                (. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
";"
(
    "forward"                                   (. factory.forwardProcedure(identifierToken, formalParameters); .)
|
                                                (. factory.startProcedureImplementation(identifierToken, formalParameters); .)
    Declarations
    Block<out StatementNode bodyNode>           (. factory.finishProcedureImplementation(bodyNode); .)
)
.


Function
=
"function"
identifier                                      (. Token identifierToken = t; .)
                                                (. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
":"
identifier                                      (. Token returnTypeToken = t; .)
";"
(
    "forward"                                   (. factory.forwardFunction(identifierToken, formalParameters, returnTypeToken); .)
|
                                                (. factory.startFunctionImplementation(identifierToken, formalParameters, returnTypeToken); .)
    Declarations
    Block<out StatementNode bodyNode>           (. factory.finishFunctionImplementation(bodyNode); .)
)
.


FormalParameterList<. out List<FormalParameter> formalParameters .>
=
"("												(. formalParameters = new ArrayList<>(); .)
												(. List<FormalParameter> newParameters = new ArrayList<>(); .)
FormalParameter<out newParameters>				(. factory.appendFormalParameter(newParameters, formalParameters); .)
{ 
	";" 
	FormalParameter<out newParameters> 			(. factory.appendFormalParameter(newParameters, formalParameters); .)
}
")"
.



FormalParameter<. out List<FormalParameter> formalParameter .>
=												(. List<String> identifiers = new ArrayList<>(); .)
												(. boolean isReference = false; .)
[ 
	"var"										(. isReference = true; .)
]
identifier										(. identifiers.add(factory.getIdentifierFromToken(t)); .)
{
	","
	identifier									(. identifiers.add(factory.getIdentifierFromToken(t)); .)
}
":"
identifier										(. formalParameter = factory.createFormalParametersList(identifiers, factory.getTypeNameFromToken(t), isReference); .)
.



MainFunction 
=
Block<out StatementNode blockNode>              (. mainNode = factory.finishMainFunction(blockNode); .)
"."
.


Block<out StatementNode blockNode>
=                                               
    "begin"                                     (. List<StatementNode> bodyNodes = new ArrayList<>(); .)
    [ 
        StatementSequence<bodyNodes>
    ]
    "end"                                       (. blockNode = factory.createBlockNode(bodyNodes); .)
.


StatementSequence<. List<StatementNode> body .>
=
Statement<out StatementNode statement>          (. body.add(statement); .)
{
    ";"
    Statement<out statement>                    (. body.add(statement); .)
}                                       
.


Statement<out StatementNode statement>
=                                               (. statement = null; .)
(
                                                (. statement = factory.createNopStatement(); .)
|
    IfStatement<out statement>
|
	ForLoop<out statement>
|
	WhileLoop<out statement>
|
	RepeatLoop<out statement> 
|
	CaseStatement<out statement>
|
	"break"										(. statement = factory.createBreak(); .)
|
    Block<out statement>
|
    IdentifierBeginningStatement<out statement>
)
.


IdentifierBeginningStatement<out StatementNode statement>
=                                               (. statement = null; .)
identifier                                      (. Token identifierToken = t; .)
(
                                                (. statement = factory.createSubroutineCall(identifierToken, new ArrayList<>()); .)
|
    SubroutineCall<out statement, identifierToken>
|
    InnerAccessRoute<. out List<AccessRouteNode> accessRoute .>
    ":="
    Expression<out ExpressionNode value>        (. statement = factory.createAssignmentWithRoute(identifierToken, accessRoute, value); .)
)
.


InnerAccessRoute<. out List<AccessRouteNode> accessRoute .>
=                                               (. accessRoute = new ArrayList<>(); .)
{
    InnerAccessRouteElement<out AccessRouteNode element>
                                                (. accessRoute.add(element); .)
}
.


InnerAccessRouteNonEmpty<. out List<AccessRouteNode> accessRoute .>
=                                               (. accessRoute = new ArrayList<>(); .)
InnerAccessRouteElement<out AccessRouteNode element>
                                                (. accessRoute.add(element); .)
{
    InnerAccessRouteElement<out element>
                                                (. accessRoute.add(element); .)
}
.


InnerAccessRouteElement<out AccessRouteNode element>
=                                               (. element = null; .)
(
    ArrayIndex<. out List<ExpressionNode> indexNodes .>
                                                (. element = new AccessRouteNode.ArrayIndex(indexNodes); .)
|
    "."
    identifier                                  (. String variableIdentifier = factory.getIdentifierFromToken(t); .)
                                                (. element = new AccessRouteNode.EnterRecord(variableIdentifier); .)
)
.


CaseStatement<out StatementNode statement>
=
"case"
Expression<out ExpressionNode caseExpression>
"of"
CaseList<out CaseStatementData caseData>        (. caseData.caseExpression = caseExpression; .)
"end"											(. statement = factory.createCaseStatement(caseData); .)
.



CaseList<out CaseStatementData data>
=                                               (. data = new CaseStatementData(); .)
Expression<out ExpressionNode caseConstant>     (. data.indexNodes.add(caseConstant); .)
":"
Statement<out StatementNode caseStatement>		(. data.statementNodes.add(caseStatement); .)
{
	IF(!caseEnds())
	";"
	Expression<out caseConstant>                (. data.indexNodes.add(caseConstant); .)
	":"
	Statement<out caseStatement>				(. data.statementNodes.add(caseStatement); .)
}
[ ";" ]
[
	"else"
	Statement<out data.elseNode>
]
[ ";" ]
.



ForLoop<out StatementNode statement>
=                                               (. factory.startLoop(); .)
"for"											(. boolean ascending = true; .)
identifier										(. Token variableToken = t; .)
":="
Expression<out ExpressionNode startValue>	
(
	"to"										(. ascending = true; .)
	|
	"downto"									(. ascending = false; .)
)
Expression<out ExpressionNode finalValue>
"do"
Statement<out StatementNode loopBody>			(. statement = factory.createForLoop(ascending, variableToken, startValue, finalValue, loopBody); .)
                                                (. factory.finishLoop(); .)
.	


RepeatLoop<out StatementNode statement>
=												(. factory.startLoop(); .)
"repeat"										(. List<StatementNode> bodyNodes = new ArrayList<>(); .)
StatementSequence<bodyNodes>					
"until"
Expression<out ExpressionNode condition>		(. statement = factory.createRepeatLoop(condition, factory.createBlockNode(bodyNodes)); .)
                                                (. factory.finishLoop(); .)
.


WhileLoop<out StatementNode statement>
=												(. factory.startLoop(); .)
"while"
Expression<out ExpressionNode condition>
"do"
Statement<out StatementNode loopBody>			(. statement = factory.createWhileLoop(condition, loopBody); .)
												(. factory.finishLoop(); .)
.


IfStatement<out StatementNode statement>
=
"if"                                            
    Expression<out ExpressionNode condition>

"then"                                          
    Statement<out StatementNode thenStatement>
                                                (. StatementNode elseStatement = null; .)
[
"else"                                          
    Statement<out elseStatement>
]                                               (. statement = factory.createIfStatement(condition, thenStatement, elseStatement); .)
.


Expression<out ExpressionNode expression>
=
LogicTerm<out expression>
{
    "or"                                        (. Token op = t; .)
    LogicTerm<out ExpressionNode right>         (. expression = factory.createBinaryExpression(op, expression, right); .)
}
.


LogicTerm<out ExpressionNode expression>
=
SignedLogicFactor<out expression>
{
    "and"                                       (. Token op = t; .)
    SignedLogicFactor<out ExpressionNode right> (. expression = factory.createBinaryExpression(op, expression, right); .)
}
.


SignedLogicFactor<out ExpressionNode expression>
=												(. expression = null; .)
(
	"not"										(. Token op = t; .)
	SignedLogicFactor<out ExpressionNode right>	(. expression = factory.createUnaryExpression(op, right); .)
|
	LogicFactor<out expression>
)
.



LogicFactor<out ExpressionNode expression>
=
Arithmetic<out expression>
[
    (">" | ">=" | "<" | "<=" | "=" | "<>" | "in" | "><" )
                                                (. Token op = t; .)
    Arithmetic<out ExpressionNode right>        (. expression = factory.createBinaryExpression(op, expression, right); .)
]
.


Arithmetic<out ExpressionNode expression>
=
Term<out expression>
{
    ( "+" | "-" )                               (. Token op = t; .)
    Term<out ExpressionNode right>              (. expression = factory.createBinaryExpression(op, expression, right); .)
}
.


Term<out ExpressionNode expression>
=
SignedFactor<out expression>
{
    ( "*" | "/" | "div" | "mod")                (. Token op = t; .)
    SignedFactor<out ExpressionNode right>      (. expression = factory.createBinaryExpression(op, expression, right); .)
}
.


SignedFactor<out ExpressionNode expression>
=                                               (. expression = null; .)
(
    ( "+" | "-" )                               (. Token unOp = t; .)
    SignedFactor<out expression>                (.  expression = factory.createUnaryExpression(unOp, expression); .)
|
    Factor<out expression>                                        
)
.



Factor<out ExpressionNode expression>
=							                    (. expression = null; .)
(
	IdentifierAccess<out expression>
|
    "("
    Expression<out expression>                  
    ")"
|
                                                (. String value = ""; .)
	StringLiteral<out value>		            (. expression = factory.createCharOrStringLiteralNode(value); .)
|
	doubleLiteral								(. expression = factory.createFloatLiteralNode(t); .)
|
    integerLiteral                              (. expression = factory.createNumericLiteralNode(t); .)
|
												(. boolean val; .)
    LogicLiteral<out val>						(. expression = factory.createLogicLiteralNode(val); .)
|
    SetConstructor<out expression>
)
.


IdentifierAccess<out ExpressionNode expression>
=
identifier                                      (. Token identifierToken = t; expression = null; .)
(
                                                (. expression = factory.createExpressionFromSingleIdentifier(identifierToken); .)
|
    InnerIdentifierAccess<out expression, identifierToken>
)
.


InnerIdentifierAccess<out ExpressionNode expression, Token identifierToken>
=							                    (. expression = null; .)
(
	SubroutineCall<out expression, identifierToken>
|
	InnerAccessRouteNonEmpty<. out List<AccessRouteNode> accessRoute .>
	                                            (. expression = factory.createExpressionFromIdentifierWithRoute(identifierToken, accessRoute); .)
)
.


SubroutineCall<out ExpressionNode expression, Token identifierToken>
=
"("
                                                (. List<ExpressionNode> parameters = new ArrayList<>(); .)
    [
        ActualParameters<out parameters, identifierToken>
    ]
")"                                             (. expression = factory.createSubroutineCall(identifierToken, parameters); .)
.


StringLiteral<out String value>
=
stringLiteral                                   (. value = factory.createStringFromToken(t); .)
.


LogicLiteral<out boolean result>
=												(. result = false; .)
(
	"true"										(. result = true; .)
|
	"false"										(. result = false; .)
)
.


SetConstructor<out ExpressionNode expression>
=                                               (. expression = null; .)
"["
    (
                                                (. expression = factory.createSetConstructorNode(new ArrayList<>()); .)
    |
                                                (. List<ExpressionNode> valueNodes = new ArrayList<ExpressionNode>(); .)
        Expression<out ExpressionNode valueNode>(. valueNodes.add(valueNode); .)
        {
            ","
            Expression<out valueNode>           (. valueNodes.add(valueNode); .)
        }
                                                (. expression = factory.createSetConstructorNode(valueNodes); .)
    )
"]"
.


ActualParameters<. out List<ExpressionNode> parameters, Token subroutineToken .>
=
                                                (. parameters = new ArrayList<>(); .)
                                                (. int currentParameter = 0; .)
ActualParameter<out ExpressionNode parameter, currentParameter, subroutineToken>
                                                (. parameters.add(parameter); .)
{
    ","
    ActualParameter<out parameter, ++currentParameter, subroutineToken>
                                                (. parameters.add(parameter); .)
}
.


ActualParameter<out ExpressionNode parameter, int currentParameterIndex, Token subroutineToken>
=
                                                (. parameter = null; .)
(
    IF(factory.shouldBeReference(subroutineToken, currentParameterIndex))
    identifier                                  (. parameter = factory.createReferenceNode(t); .)
|
    Expression<out parameter>
)
.



ArrayIndex<. out List<ExpressionNode> indexNodes .>
=
												(. indexNodes = new ArrayList<>(); .)
												(. ExpressionNode indexingNode = null; .)
"["
	Expression<out indexingNode>	            (. indexNodes.add(indexingNode); .)
	{
		","
		Expression<out indexingNode>	        (. indexNodes.add(indexingNode); .)
	}
"]"
.



Unit
=
UnitHeader
InterfaceSection                                (. factory.finishUnitInterfaceSection(); .)
ImplementationSection
UnitFooter
.


UnitHeader
=
"unit"
identifier										(. factory.startUnit(t); .)
";"
.


UnitFooter
=
"end"											(. factory.endUnit(); .)
"."
.


InterfaceSection
=
"interface"
{
	(
		ProcedureHeading
	|
		FunctionHeading
	|
	    TypeDefinitions
	|
	    VariableDeclarations
	)
}
.



ProcedureHeading
=
"procedure"
identifier										(. Token name = t; .)
												(. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
												(. factory.addUnitProcedureInterface(name, formalParameters); .)
";"
.



FunctionHeading
=
"function"
identifier										(. Token name = t; .)
												(. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
":"
identifier										(. String returnTypeName = factory.getTypeNameFromToken(t); .)
												(. factory.addUnitFunctionInterface(name, formalParameters, returnTypeName); .)
";"
.



ImplementationSection
=
"implementation"
{
	(
		UnitSubroutineImplementation
	|
		VariableDeclarations
	|
		TypeDefinitions
	)
}
.


UnitSubroutineImplementation
=
(
    UnitProcedureImplementation ";"
|
    UnitFunctionImplementation ";"
)
.


UnitProcedureImplementation
=
"procedure"
identifier                                      (. Token identifierToken = t; .)
                                                (. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
";"
                                                (. factory.startUnitProcedureImplementation(identifierToken, formalParameters); .)
Declarations
Block<out StatementNode bodyNode>               (. factory.finishProcedureImplementation(bodyNode); .)
.


UnitFunctionImplementation
=
"function"
identifier                                      (. Token identifierToken = t; .)
                                                (. List<FormalParameter> formalParameters = new ArrayList<>(); .)
[ FormalParameterList<out formalParameters> ]
":"
identifier                                      (. Token returnTypeToken = t; .)
";"
                                                (. factory.startUnitFunctionImplementation(identifierToken, formalParameters, returnTypeToken); .)
Declarations
Block<out StatementNode bodyNode>               (. factory.finishFunctionImplementation(bodyNode); .)
.

END Pascal.
